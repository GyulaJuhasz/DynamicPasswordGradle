apply plugin: 'com.android.application'

task('inputKeyPasswords') {
	doLast {
		def keyStorePassword = null
		while (!(keyStorePassword instanceof String) || ((String) keyStorePassword).isEmpty()) {
			keyStorePassword = inputPassword('KeyStore password', 'Please enter the password for the keystore:')
		}

		def keyPassword = null
		while (!(keyPassword instanceof String) || ((String) keyPassword).isEmpty()) {
			keyPassword = inputPassword('Key password', 'Please enter the password for the signing key:')
		}

		android.signingConfigs.releaseSigningConfig.storePassword = keyStorePassword
		android.signingConfigs.releaseSigningConfig.keyPassword = keyPassword
	}
}

tasks.whenTaskAdded { task ->
	if (task.name.trim() == 'validateSigningRelease') {
		task.dependsOn inputKeyPasswords
	}
}

android {
	compileSdkVersion 25
	buildToolsVersion "25.0.3"
	defaultConfig {
		applicationId "com.gyulajuhasz.example.dynamicpasswordinput"
		minSdkVersion 19
		targetSdkVersion 25
		versionCode 1
		versionName "1.0"
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		releaseSigningConfig {
			storeFile file('ExampleKeyStore')
			storePassword ''
			keyAlias 'examplekey'
			keyPassword ''
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.releaseSigningConfig
		}
	}

}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])
	compile 'com.android.support:appcompat-v7:25.3.1'
	compile 'com.android.support.constraint:constraint-layout:1.0.2'
	compile 'com.android.support:design:25.3.1'
}
